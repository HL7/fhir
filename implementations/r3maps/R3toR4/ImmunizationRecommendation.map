map "http://hl7.org/fhir/StructureMap/ImmunizationRecommendation3to4" = "R3 to R4 Conversions for ImmunizationRecommendation"

uses "http://hl7.org/fhir/3.0/StructureDefinition/ImmunizationRecommendation" alias ImmunizationRecommendationR3 as source
uses "http://hl7.org/fhir/StructureDefinition/ImmunizationRecommendation" alias ImmunizationRecommendation as target

imports "http://hl7.org/fhir/StructureMap/*3to4"

group ImmunizationRecommendation(source src : ImmunizationRecommendationR3, target tgt : ImmunizationRecommendation) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.patient -> tgt.patient;
  src.recommendation as s -> tgt.recommendation as t then recommendation(s, t, tgt);
}

group recommendation(source src, target tgt, target res) extends BackboneElement {
  src.date -> res.date;
  src.vaccineCode -> tgt.vaccineCode;
  src.targetDisease -> tgt.targetDisease;
  src.doseNumber as vs -> tgt.doseNumber = create('positiveInt') as vt then positiveInt(vs, vt);
  src.forecastStatus -> tgt.forecastStatus;
  src.dateCriterion as s -> tgt.dateCriterion as t then dateCriterion(s, t);
  src.protocol as s then {
    s.description -> tgt.description;
    s.doseSequence as vs -> tgt.seriesDoses = create('positiveInt') as vt then positiveInt(vs, vt);
    s.authority -> res.authority;
    s.series -> tgt.series;
  };
  src.series -> tgt.series;
  src.supportingImmunization -> tgt.supportingImmunization;
  src.supportingPatientInformation -> tgt.supportingPatientInformation;
}

group dateCriterion(source src, target tgt) extends BackboneElement {
  src.code -> tgt.code;
  src.value -> tgt.value;
}

