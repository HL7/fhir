map "http://hl7.org/fhir/StructureMap/QuestionnaireResponse3to4" = "R3 to R4 Conversions for QuestionnaireResponse"

uses "http://hl7.org/fhir/3.0/StructureDefinition/QuestionnaireResponse" alias QuestionnaireResponseR3 as source
uses "http://hl7.org/fhir/StructureDefinition/QuestionnaireResponse" alias QuestionnaireResponse as target

imports "http://hl7.org/fhir/StructureMap/*3to4"

group QuestionnaireResponse(source src : QuestionnaireResponseR3, target tgt : QuestionnaireResponse) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.basedOn -> tgt.basedOn;
  src.parent -> tgt.partOf;
  src.questionnaire -> tgt.questionnaire;
  src.status -> tgt.status;
  src.subject -> tgt.subject;
  src.context -> tgt.encounter;
  src.authored -> tgt.authored;
  src.author -> tgt.author;
  src.source -> tgt.source;
  src.item as vs0 -> tgt.item as vt0 then item(vs0, vt0);
}

group item(source src, target tgt) extends BackboneElement {
  src.linkId -> tgt.linkId;
  src.definition -> tgt.definition;
  src.text -> tgt.text;
  src.subject as v ->  tgt.extension as vt,  vt.url = 'http://hl7.org/fhir/3.0/StructureDefinition/extension-QuestionnaireResponse.item.subject',  vt.value = v;
  src.answer as vs0 -> tgt.answer as vt0 then answer(vs0, vt0);
  src.item as vs0 -> tgt.item as vt0 then item(vs0, vt0);
}

group answer(source src, target tgt) extends BackboneElement {
  src.value -> tgt.value;
  src.item as vs0 -> tgt.item as vt0 then item(vs0, vt0);
}

