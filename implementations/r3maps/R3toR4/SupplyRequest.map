map "http://hl7.org/fhir/StructureMap/SupplyRequest3to4" = "R3 to R4 Conversions for SupplyRequest"

uses "http://hl7.org/fhir/3.0/StructureDefinition/SupplyRequest" alias SupplyRequestR3 as source
uses "http://hl7.org/fhir/StructureDefinition/SupplyRequest" alias SupplyRequest as target

imports "http://hl7.org/fhir/StructureMap/*3to4"

group SupplyRequest(source src : SupplyRequestR3, target tgt : SupplyRequest) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status -> tgt.status;
  src.category -> tgt.category;
  src.priority -> tgt.priority;
  src.orderedItem as s then {
    s.item : CodeableConcept as vs -> tgt.item = create('CodeableConcept') as vt then CodeableConcept(vs, vt);
    s.item : Reference as vs -> tgt.item = create('Reference') as vt then Reference(vs, vt);
    s.quantity -> tgt.quantity;
  };
  src.occurrence : dateTime as vs -> tgt.occurrence = create('dateTime') as vt then dateTime(vs, vt);
  src.occurrence : Period as vs -> tgt.occurrence = create('Period') as vt then Period(vs, vt);
  src.occurrence : Timing as vs -> tgt.occurrence = create('Timing') as vt then Timing(vs, vt);
  src.authoredOn -> tgt.authoredOn;
  src.requester as vs0 -> tgt then agent(vs0, tgt);
  src.supplier -> tgt.supplier;
  src.reason : CodeableConcept as vs -> tgt.reasonCode as vt then CodeableConcept(vs, vt);
  src.reason : Reference as vs -> tgt.reasonReference as vt then Reference(vs, vt);
  src.deliverFrom -> tgt.deliverFrom;
  src.deliverTo -> tgt.deliverTo;
}

group agent(source src, target tgt) {
  src.agent -> tgt.requester;
  src.onBehalfOf as v ->  tgt.extension as vt,  vt.url = 'http://hl7.org/fhir/3.0/StructureDefinition/extension-SupplyRequest.requester.onBehalfOf',  vt.value = v;
}

